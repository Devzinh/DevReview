plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "com.gradleup.shadow" version "8.3.0"
}

group = 'br.com.devplugins'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven { url 'https://jitpack.io' }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT")
    implementation "org.bstats:bstats-bukkit:3.0.2"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly "me.clip:placeholderapi:2.11.6"
    compileOnly "org.jetbrains:annotations:24.0.0"
    
    // Property-based testing with jqwik
    testImplementation "net.jqwik:jqwik:1.8.2"
    testImplementation "org.junit.jupiter:junit-jupiter:5.10.1"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    
    // Mockito for mocking Bukkit components
    testImplementation "org.mockito:mockito-core:5.8.0"
    testImplementation "org.mockito:mockito-junit-jupiter:5.8.0"
    
    // Spigot API for tests
    testImplementation("org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT")
}

tasks {
    runServer {
        minecraftVersion("1.21")
    }
    
    test {
        useJUnitPlatform {
            includeEngines "jqwik", "junit-jupiter"
        }
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat "full"
        }
    }
}

shadowJar {
    relocate 'org.bstats', 'br.com.devplugins.shadow.bstats'
    archiveClassifier.set('')
}

build {
    dependsOn shadowJar
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
